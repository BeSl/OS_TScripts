//********************************************************************************
Функция ПолучитьВерсиюИзФайлаНастроек(ПолноеИмяФайла)

	Версия = 0;
	ФайлНастроек = Новый Файл(ПолноеИмяФайла);
	Если Не ФайлНастроек.Существует() Тогда
		Возврат 0;
	КонецЕсли;

	ЧтениеТекста = Новый ЧтениеТекста(ФайлНастроек.ПолноеИмя);
	ТекущаяСтрока = ЧтениеТекста.ПрочитатьСтроку();
	Пока ТекущаяСтрока <> Неопределено Цикл
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока);
		Если Лев(ТекущаяСтрока,7) = ".Версия" Тогда
			Версия = Сред(ТекущаяСтрока,10);
			Версия = Лев(Версия,СтрДлина(Версия)- 2);
		ИначеЕсли Лев(ТекущаяСтрока,13) = ".Р’РµСЂСЃРёСЏ" Тогда
			Версия = Сред(ТекущаяСтрока,16);
			Версия = Лев(Версия,СтрДлина(Версия)- 2);
		КонецЕсли;
		ТекущаяСтрока = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла;
	ЧтениеТекста.Закрыть();

	Возврат Версия;

КонецФункции

//********************************************************************************
Функция ПолучитьФайлСgithub(ПутьКфайлу,ИмяФайла)

	Соединение = Новый HTTPСоединение(ПутьКфайлу);
	
	Запрос = Новый HTTPЗапрос(ИмяФайла);
	Ответ = Соединение.Получить(Запрос);

	Если Ответ = Неопределено Тогда
		Сообщить("ОШИБКА: Не удалось получить файл """ + ИмяФайла + """ с github.com");
		Возврат Неопределено;
	КонецЕсли;

	ИмяСкаченногоФайла = ОбъединитьПути(КаталогВременныхФайлов(), ИмяФайла);
	Ответ.ПолучитьТелоКакДвоичныеДанные().Записать(ИмяСкаченногоФайла);
	Ответ.Закрыть();

	СкаченныйФайл = Новый файл(ИмяСкаченногоФайла);
	Если Не СкаченныйФайл.Существует() Тогда
		Сообщить("ОШИБКА: Не удалось получить файл """ + ИмяФайла + """ с github.com");
		Возврат Неопределено;
	КонецЕсли;

	Возврат ИмяСкаченногоФайла;

КонецФункции

//********************************************************************************
Процедура ВыполнитьОбработку(ПутьКТекущемуКаталогу="", Знач Версия=0)

	Сообщить("-----------------------------------");
	Сообщить("НАЧАЛО ВЫПОЛНЕНИЯ ОБРАБОТКИ ""package-install""");
	Сообщить("-----------------------------------");

	ФайлУстановки = Неопределено;
	НеобходимоУдалитьФайлУстановки = Ложь;

	Если Версия = 0 Тогда
		Сообщить("Версия пакета для установки не задана");
	Иначе
		Сообщить("Версия пакета для установки: " + Версия);
	КонецЕсли;

	// Получим версию библиотек из текущего каталога
	Если ПутьКТекущемуКаталогу = "" Тогда		
		Сообщить("Каталог с файлами установки не задан");	
	Иначе
		Сообщить("Каталог с файлами установки: " + ПутьКТекущемуКаталогу);

		Если Версия = 0 Тогда

			Версия = ПолучитьВерсиюИзФайлаНастроек(ОбъединитьПути(ПутьКТекущемуКаталогу,"packagedef"));
			
			Если Версия = 0 Тогда

				// Может быть в вышестоящем каталоге?
				КаталогСФайломНастроек = ПутьКТекущемуКаталогу;
				НомерВхождения = СтрНайти(ПутьКТекущемуКаталогу,"\Distr");
				Если НомерВхождения > 0 Тогда
					КаталогСФайломНастроек = Лев(ПутьКТекущемуКаталогу,НомерВхождения-1);
					Сообщить("Поиск файлов установки в каталоге: " + КаталогСФайломНастроек);
					Версия = ПолучитьВерсиюИзФайлаНастроек(ОбъединитьПути(КаталогСФайломНастроек,"packagedef"));
				КонецЕсли;

			КонецЕсли;
			
			Если Версия = 0 Тогда
				Сообщить("Не удалось найти файлы установки пакета в текущем каталоге");
			Иначе
				Сообщить("Версия пакета для установки: " + Версия);
			КонецЕсли;
		
		КонецЕсли;

	КонецЕсли;

	// Получим версию библиотек с github.com
	Если Версия = 0 Тогда

		ИмяФайлаНастроек = ПолучитьФайлСgithub("https://github.com/Tavalik/OS_TScripts/raw/master/","packagedef");
		Если ИмяФайлаНастроек = Неопределено Тогда
			//Сообщить("ОШИБКА: Не удалось загрузить файл настроек");
			Возврат;
		КонецЕсли;

		Сообщить("Загружен файл настроек: " + ИмяФайлаНастроек);
		Версия = ПолучитьВерсиюИзФайлаНастроек(ИмяФайлаНастроек);
		Если Версия = 0 Тогда
			Сообщить("ОШИБКА: Не удалось получить версию пакета для установки");
			Возврат;
		КонецЕсли;

		Сообщить("Версия пакета для установки: " + Версия);
		УдалитьФайлы(ИмяФайлаНастроек);
		Сообщить("Удален файл настроек: " + ИмяФайлаНастроек);

	КонецЕсли;

	// Найдем файл установки в текущем каталоге
	Если Версия <> 0 Тогда
		ФайлУстановки = Новый файл(ОбъединитьПути(ПутьКТекущемуКаталогу,"Tlib-" + Версия + ".ospx"));
		Если Не ФайлУстановки.Существует() Тогда
			ФайлУстановки = Неопределено;
		Иначе
			ФайлУстановки = ФайлУстановки.ПолноеИмя;
			Сообщить("Найден файл установки: " + ФайлУстановки);
		КонецЕсли;
	КонецЕсли;

	// Если в текущем каталоге не найден нужный файл установщика, скачаем его с github
	Если Версия <> 0 И ФайлУстановки = Неопределено Тогда

		Сообщить("Получаем файлы установки с https://github.com/Tavalik/OS_TScripts");

		ФайлУстановки = ПолучитьФайлСgithub("https://github.com/Tavalik/OS_TScripts/raw/master/","Tlib-" + Версия + ".ospx");
		Если ФайлУстановки = Неопределено Тогда
			//Сообщить("ОШИБКА: Не удалось загрузить файл установки");
			Возврат;
		Иначе
			Сообщить("Загружен файл установки: " + ФайлУстановки);
			НеобходимоУдалитьФайлУстановки = Истина;
		КонецЕсли;

	КонецЕсли;

	// Не удалось получить версию и файл установки. Найдем хоть что-то.
	Если ФайлУстановки = Неопределено Тогда 
		МассивФайлов = НайтиФайлы(ПутьКТекущемуКаталогу,"Tlib-*.ospx");
		Для Каждого НайденныйФайл из МассивФайлов Цикл
			ФайлУстановки = НайденныйФайл.ПолноеИмя;
			Сообщить("В текущем каталоге найден файл установки: " + ФайлУстановки);
		КонецЦикла;
	КонецЕсли;

	Если ФайлУстановки = Неопределено Тогда

		Сообщить("ОШИБКА: Не найден файл установки");
		Возврат;

	Иначе

		КаталогПрограммы = КаталогПрограммы();
		НомерВхождения = СтрНайти(КаталогПрограммы,"\bin");
		Если НомерВхождения > 0 Тогда
			КаталогПрограммы = Лев(КаталогПрограммы,НомерВхождения-1);
		КонецЕсли;		

		КодВозврата = 0;
		Команда = "oscript """ + КаталогПрограммы + "\lib\opm\src\opm.os"" install -f """ + СокрЛП(ФайлУстановки) + """";
		Сообщить("Выполняем команду: "+ Команда);
		ЗапуститьПриложение(Команда,,Истина,КодВозврата);
		Если КодВозврата <> 0 Тогда
			Если КодВозврата = 255 Тогда
				Сообщить("ОШИБКА: Установка файла поставки не выполнена. Запустите файл установки с правами локального Администратора");
			Иначе
				Сообщить("ОШИБКА: Установка файла поставки не выполнена. Код ошибки - " + КодВозврата);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		Сообщить("Установлен файл установки: " + ФайлУстановки);

		Если НеобходимоУдалитьФайлУстановки Тогда
			УдалитьФайлы(ФайлУстановки);
			Сообщить("Удален файл установки: " + ФайлУстановки);
		КонецЕсли;

		Сообщить("-----------------------------------");
		Сообщить("УСПЕШНОЕ ВЫПОЛНЕНИЕ ОБРАБОТКИ ""package-install""!");
		Сообщить("-----------------------------------");

	КонецЕсли;

КонецПроцедуры

ПутьКТекущемуКаталогу = ТекущийСценарий().Каталог;
ВыполнитьОбработку(ПутьКТекущемуКаталогу);