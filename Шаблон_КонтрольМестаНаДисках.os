//*****************************************************************
// Автор: Онянов Виталий (Tavalik.ru)
// Описание: 
//	Скрипт высчитывает процент свободного места на дисках и отправляет уведомление
//			на электронную почту, если превышен указанный порог.
//			Порог (в %) задается в теле модуля или указывается в виде аргумента командной строки.
// Аргументы командной строки:
//		- КритическийПорог - Число - критический порог свободного места на дисках в процентах

//***************************************************************
// ПОДКЛЮЧАЕМЫЕ БИБЛИОТЕКИ

//Логирование
#Использовать "TLog"
//Электронная почта 
#Использовать "TMail" 
//Файловые операции
#Использовать "TFile" 

//***************************************************************
// АРГУМЕНТЫ КОМАНДНОЙ СТРОКИ

Если АргументыКоманднойСтроки.Количество() > 0 Тогда
	КритическийПорог = Число(АргументыКоманднойСтроки[0]);
Иначе
	КритическийПорог = 10;
КонецЕсли;

//***************************************************************
// НАСТРАИВАЕМЫЕ ПАРАМЕТРЫ

// Каталог для хранения логов
КаталогХраненияЛогов = ".\_Logs\";
ХранитьЛогиДней = 365;

// Буквы дисков для контроля
БуквыДисков = "";  //Все, для указания конкретных дисков, строка вида "CDE"

// Инициируем параметры для отправки сообщения
УправлениеЭП = Новый ТУправлениеЭлектроннойПочтой(); //TMail
УчетнаяЗаписьЭП = УправлениеЭП.УчетнаяЗаписьЭП;
УчетнаяЗаписьЭП.АдресSMTP = "smtp.mydomen.com";
УчетнаяЗаписьЭП.ПортSMTP = 465;  
УчетнаяЗаписьЭП.ПользовательSMTP = "report@mydomen.com";
УчетнаяЗаписьЭП.ПарольSMTP = "pass_mail";
УчетнаяЗаписьЭП.ИспользоватьSSL = Истина;

СтруктураСообщения = УправлениеЭП.СтруктураСообщения;
СтруктураСообщения.АдресЭлектроннойПочтыПолучателя = "admin@mydomen.com;";
СтруктураСообщения.ТипТекстаПочтовогоСообщения = "HTML";

//***************************************************************
// ТЕЛО СКРИПТА

// Сделаем записть о начале выполнения задания
Логирование = Новый ТУправлениеЛогированием(); //TLog
Логирование.СоздатьФайлЛога("КонтрольМестаНаДисках",КаталогХраненияЛогов);
Логирование.ЗаписатьСтрокуЛога(Строка(ТекущаяДата()) + ": Начало выполнения задания ""КонтрольМестаНаДисках""");
Логирование.ЗаписатьСтрокуЛога();
Логирование.УвеличитьУровень();

// Служебные переменные
БылиОшибки = Ложь;
ЕстьНеПрошедшиеПорог = Ложь;
СИ = Новый СистемнаяИнформация();

// Соберем информацию о дисках
ФайловыеОперации = Новый ТУправлениеФайловымиОперациями(); //TFile
ТаблицаДанных = ФайловыеОперации.ИнформацияОДисках(БуквыДисков);	
Если ТаблицаДанных = Неопределено ИЛИ ТаблицаДанных.Количество() = 0 Тогда
	// Не удалось собрать данные
	БылиОшибки = Истина;
	// Запишем в лог
	Логирование.ЗаписатьСтрокуЛога("ОШИБКА выполнения задания ""КонтрольМестаНаДисках""");
	Логирование.ЗаписатьСтрокуЛога("	Текст ошибки: " + ФайловыеОперации.ТекстОшибки);
	// Данные сообщения
	СтруктураСообщения.ТемаСообщения = "ВНИМАНИЕ! Не удалось выполнить контроль свободного места на сервере " + СИ.ИмяКомпьютера;
	УправлениеЭП.НачатьТекстСообщенияHTML();
	УправлениеЭП.ДобавитьВТекстСообщенияHTML();
	УправлениеЭП.ДобавитьВТекстСообщенияHTML("ОШИБКА выполнения задания ""КонтрольМестаНаДисках""");
	УправлениеЭП.ДобавитьВТекстСообщенияHTML("	Текст ошибки: " + ФайловыеОперации.ТекстОшибки);
	УправлениеЭП.ДобавитьВТекстСообщенияHTML();
Иначе
	// Данные собраны, текст будущего сообщения
	СтруктураСообщения.ТемаСообщения = "ВНИМАНИЕ! Заканчивается место на сервере " + СИ.ИмяКомпьютера;
	УправлениеЭП.НачатьТекстСообщенияHTML();
	УправлениеЭП.ДобавитьВТекстСообщенияHTML();
	
	Для Каждого Данные Из ТаблицаДанных Цикл
	
		Если Данные.ДоступноМб = 0 Тогда
			Продолжить;
		КонецЕсли;

		СвободноПроцент = Окр(Данные.СвободноМб / Данные.ОбщийОбъемМб * 100,2);
		Если СвободноПроцент <= КритическийПорог Тогда

			// Есть диски, которые не прошли контроль
			Если Не ЕстьНеПрошедшиеПорог Тогда
				Логирование.ЗаписатьСтрокуЛога("Диски не прошедшие контроль:");
			КонецЕсли;
			ЕстьНеПрошедшиеПорог = Истина;

			// Запишем сначала в лог
			Логирование.ЗаписатьСтрокуЛога();
			Логирование.ЗаписатьСтрокуЛога("Буква диска: " + Данные.Буква);					
			Логирование.ЗаписатьСтрокуЛога("Всего МБ: " + Данные.ОбщийОбъемМб);
			Логирование.ЗаписатьСтрокуЛога("Свободно МБ: " + Данные.СвободноМб);
			Логирование.ЗаписатьСтрокуЛога("Свободно %: " + СвободноПроцент);
		
			// Формируем электронное сообщение
			УправлениеЭП.ДобавитьВТекстСообщенияHTML("Буква диска: <font size=""5"" color=""green""><b>" + Данные.Буква + "</b></font>");
			УправлениеЭП.ДобавитьВТекстСообщенияHTML("Всего: " + Формат(Данные.ОбщийОбъемМб,"ЧРГ=' '") + " МБ"); 
			УправлениеЭП.ДобавитьВТекстСообщенияHTML("Свободно: " + Формат(Данные.СвободноМб,"ЧРГ=' '") + " МБ");
			УправлениеЭП.ДобавитьВТекстСообщенияHTML("Свободно в процентах: <font size=""5"" color=""red"">" + СвободноПроцент + "%</font>");
			УправлениеЭП.ДобавитьВТекстСообщенияHTML();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецЕсли;

Если Не БылиОшибки И Не ЕстьНеПрошедшиеПорог Тогда
	// Все диски прошли контроль
	Логирование.ЗаписатьСтрокуЛога("Все диски прошли контроль, критический порог: " + КритическийПорог);
Иначе
	
	// Отправим сообщение
	Логирование.ЗаписатьСтрокуЛога();
	Логирование.ЗаписатьСтрокуЛога("Попытка отправки электронного сообщения:");
	УправлениеЭП.ЗавершитьТекстСообщенияHTML();	
	Если УправлениеЭП.ОтправитьСообщение() Тогда
		Логирование.ЗаписатьСтрокуЛога("Отправлено электорнное сообщение на адреса: " + СтруктураСообщения.АдресЭлектроннойПочтыПолучателя);
	Иначе
		Логирование.ЗаписатьСтрокуЛога("ОШИБКА: Не удалось отправить электронное сообщение с smtp-сервера: " + УчетнаяЗаписьЭП.АдресSMTP);
		Логирование.ЗаписатьСтрокуЛога("	по причине " + УправлениеЭП.ТекстОшибки);
		БылиОшибки = Истина;
	КонецЕсли;
	
КонецЕсли;	

// Удалим все логи за последний год
ФайловыеОперации.УдалитьФайлыСозданныеБолееДнейНазад(КаталогХраненияЛогов + "\КонтрольМестаНаДисках",ХранитьЛогиДней);

// Сделаем записть о завершении выполнения задания
Логирование.ЗаписатьСтрокуЛога();
Логирование.ЗаписатьСтрокуЛога("Результат выполнения задания: " + ?(БылиОшибки,"БЫЛИ ОШИБКИ","УСПЕШНОЕ ВЫПОЛНЕНИЕ"));
Логирование.ЗаписатьСтрокуЛога();
Логирование.УменьшитьУровень();
Логирование.ЗаписатьСтрокуЛога(Строка(ТекущаяДата()) + ": Завершение выполнения задания ""КонтрольМестаНаДисках""");





